plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.google.protobuf' version '0.8.18'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

tasks.named('jib').configure {
    dependsOn 'build'
}

def getGitCommitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

jib {
    to {
        image = "glad2os/clh-bookstore:${version}-${getGitCommitHash()}"
    }
}

group = 'clh.comp313'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.58.0'
    protobufVersion = '3.23.4'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // gRPC dependencies
    implementation 'com.google.protobuf:protobuf-java-util:3.25.2'
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"

    // tests
    implementation platform('org.testcontainers:testcontainers-bom:1.19.3') //import bom
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
    testImplementation "org.testcontainers:testcontainers:1.19.3"
    testImplementation "org.testcontainers:junit-jupiter:1.19.3"
    implementation 'com.redis.testcontainers:testcontainers-redis:1.6.4'

    //cache
    implementation 'redis.clients:jedis:5.1.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        ofSourceSet('main').forEach {
            it.plugins {
                grpc {}
            }
        }
    }
}

test {
    useJUnitPlatform()
}


sourceSets {
    main {
        java {
            srcDir "${protobuf.generatedFilesBaseDir}/main/grpc"
            srcDir "${protobuf.generatedFilesBaseDir}/main/java"
        }
    }
}

task cleanGenerated {
    doLast {
        file("${protobuf.generatedFilesBaseDir}/main").deleteDir()
    }
}

clean.dependsOn cleanGenerated
